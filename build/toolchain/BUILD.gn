toolchain("clang") {
  _compile_common = {
    depfile = "{{output}}.d"
    depsformat = "gcc"
    switches = "-MD -MF $depfile -o {{output}} {{defines}} {{include_dirs}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }


  tool("cc") {
    description = "CC {{output}}"
    forward_variables_from(_compile_common, "*")
    command = "clang $switches {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
  }

  tool("cxx") {
    description = "CXX {{output}}"
    forward_variables_from(_compile_common, "*")
    command = "clang++ $switches {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
  }

  tool("asm") {
    description = "ASM {{output}}"
    forward_variables_from(_compile_common, "*")
    command = "clang $switches {{asmflags}} -c {{source}}"
  }

  tool("alink") {
    description = "AR {{target_output_name}}{{output_extension}}"
    command = "ar rcs {{output}} {{inputs}}"
    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }

  tool("link") {
    outfile = "{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    command = "clang++ {{ldflags}} -o $outfile -Wl,--start-group @$rspfile -Wl,--end-group {{libs}}"
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    rspfile_content = "{{inputs}}"
    outputs = [ outfile ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
